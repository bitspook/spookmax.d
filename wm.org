#+PROPERTY: header-args :tangle ./wm.el

- Load the common code

  #+begin_src elisp
    (load-file (expand-file-name "./common.el" user-emacs-directory))
  #+end_src

- EXWM config copied from wiki

  #+begin_src elisp
    (use-package exwm
      :config
      (setq exwm-workspace-number 5)

      (add-hook 'exwm-update-class-hook
                (lambda ()
                  (unless (string-prefix-p "sun-awt-X11-" exwm-instance-name)
                    (exwm-workspace-rename-buffer exwm-class-name))))
      (add-hook 'exwm-update-title-hook
                (lambda ()
                  (when (or (not exwm-instance-name)
                            (string-prefix-p "sun-awt-X11-" exwm-instance-name))
                    (exwm-workspace-rename-buffer exwm-title))))

      ;; Disabling these for now. There seem to be some bug which is causing
      ;; systemtray to show up on an entire workspace and causing other problems.
      ;; Hoping this will fix itself in a future update
      ;; (require 'exwm-systemtray)
      ;; (exwm-systemtray-enable)

      (require 'exwm-randr)
      (exwm-randr-enable)

      ;; When on small screen, set the resolution to fullHD
      (let ((monitor (cdr (cl-assoc 'small-screen spook--active-profiles))))
        (when monitor
          (start-process "" "*wm-startup*" "xrandr" "--output" monitor "--mode" "1920x1080")))

      ;; If HDMI cable is connected, turn off laptop monitor
      (let ((hdmi (cdr (cl-assoc 'has-hdmi spook--active-profiles))))
        (when hdmi
          (start-process "" "*wm-startup*" "xrandr" "--output" hdmi "--auto" "--output" "eDP-1" "--off")
          (start-process-shell-command "" "*wm-startup*" "feh --bg-fill --randomize /home/channi/Pictures/wallpaper")))

      (setq exwm-input-global-keys
            `(
              (,(kbd "s-H") . (lambda () (interactive) (exwm-layout-shrink-window-horizontally 40)))
              (,(kbd "s-L") . (lambda () (interactive) (exwm-layout-enlarge-window-horizontally 40)))
              (,(kbd "s-J") . (lambda () (interactive) (exwm-layout-shrink-window 40)))
              (,(kbd "s-K") . (lambda () (interactive) (exwm-layout-enlarge-window 40)))
              (,(kbd "s-l") . windmove-right)
              (,(kbd "s-h") . windmove-left)
              (,(kbd "s-k") . windmove-up)
              (,(kbd "s-j") . windmove-down)
              (,(kbd "s-<tab>") . consult-buffer)
              ([?\s-r] . exwm-reset)
              ([?\s-w] . exwm-workspace-switch)
              ,@(mapcar (lambda (i)
                          `(,(kbd (format "s-%d" i)) .
                            (lambda ()
                              (interactive)
                              (exwm-workspace-switch-create ,i))))
                        (number-sequence 0 9))
              ([?\s-`] . (lambda () (interactive) (exwm-workspace-switch-create 0)))
              ([?\s-&] . (lambda (command)
                           (interactive (list (read-shell-command "$ ")))
                           (start-process-shell-command command nil command)))
              ([?\s-d] . (lambda () (interactive) (start-process-shell-command "rofi" "*wm-shortcuts*" "rofi -show run")))
              (,(kbd "s-Q") . exwm-input-toggle-keyboard)
              ([?\s-q] . exwm-input-send-next-key)
              ([?\s-f] . exwm-input-toggle-fullscreen)
              ([?\s-\ ] . ,meow-keymap)
              ([?\s-S] . (lambda () (interactive) (start-process-shell-command "flameshot" "*wm-shortcuts*" "flameshot gui")))))

      (defun spook--rofi-pass ()
        (interactive)
        (start-process-shell-command "" nil "rofi-pass"))

      (define-key exwm-mode-map (kbd "s-q") #'exwm-input-send-next-key)
      (define-key exwm-mode-map (kbd "s-P") #'spook--rofi-pass)
      (define-key exwm-mode-map (kbd "C-c m") #'exwm-workspace-move-window)

      (setq exwm-input-simulation-keys
            `(
              (,(kbd "C-b") . [left])
              (,(kbd "M-b") . [C-left])
              (,(kbd "C-f") . [right])
              (,(kbd "M-f") . [C-right])
              (,(kbd "C-p") . [up])
              (,(kbd "C-n") . [down])
              (,(kbd "C-a") . [home])
              (,(kbd "C-e") . [end])
              (,(kbd "M-v") . [prior])
              (,(kbd "C-v") . [next])
              (,(kbd "C-d") . [delete])
              (,(kbd "C-k") . [S-end delete])
              (,(kbd "C-w") . [?\C-x])
              (,(kbd "M-w") . [?\C-c])
              (,(kbd "C-y") . [?\C-v])
              (,(kbd "C-s") . [?\C-f])))

      (setq mouse-autoselect-window t
            focus-follows-mouse nil)

      (exwm-enable))
  #+end_src

- Global keybindings now that =s-= prefix is free

  #+begin_src elisp
    (global-set-key  (kbd "s-x") #'execute-extended-command)
  #+end_src

- Display batter and time in modline

  #+begin_src elisp
    (display-battery-mode +1)
    (setq display-time-day-and-date t)
    (display-time-mode +1)
  #+end_src

- Move any input to emacs for quick editing

  #+begin_src elisp
    (defun spook--on-exwm-edit-compose ()
      (funcall 'markdown-mode))

    (use-package exwm-edit
      :config
      (add-hook 'exwm-edit-compose-hook #'spook--on-exwm-edit-compose))
  #+end_src
